// Description:
//    construction d'un tableau à partir d'un arbre binaire
//
// Resultats:
//    doit fonctionner normalement
//
// Historique:
//    cree le 21/01/2012

class Integer {
	int val;

	Integer init (int val) {
		this.val = val;
		return this;
	}

	boolean equals (Object o) {
		return o != null && o instanceof Integer &&
			cast<Integer>(o).val == val;
	}
}

class Cellule {
	Object val;
	Cellule fg;
	Cellule fd;
}

class Arbre {
	protected Cellule racine;
	protected int taille;

	Cellule allouer (int taille)
		Cellule res = new Cellule();
        {
		if (taille > 1) {
			res.fg = allouer (taille - taille / 2);
			res.fd = allouer (taille / 2);
		}
		return res;
	}

	Arbre init (int taille) {
		this.taille = taille;
		racine = allouer (taille);
		return this;
	}

	int getTaille () {
		return taille;
	}

	Cellule chercher (Cellule racine, int index) {
		if (index == 0 && racine.fg == null) {
			return racine;
		} elsif (index % 2 == 0) {
			return chercher (racine.fg, index / 2);
		} else {
			return chercher (racine.fd, index / 2);
		}
	}

	Object get (int index) {
		if (index < 0 || index >= taille) {return null;}
		return chercher(racine, index).val;
	}

	Object set (int index, Object val) {
		if (index < 0 || index >= taille) {return null;}
		return chercher(racine, index).val = val;
	}
}

int taille;
boolean ok = false;
Arbre tab;
int index;
{
	while (! ok) {
		print("Taille du tableau pour le test ? ");
		taille = readInt();
		if (taille < 0) {
			println("Taille incorrecte, recommencez");
			ok = false;
		} else {
			ok = true;
		}
	}
	
	print("Allocation d'un tableau de taille ", taille, "... ");
	tab = new Arbre().init(taille);
	println("OK");

	print("Insertion dans le tableau... ");
	index = 0;
	while (index < taille) {
		tab.set(index, new Integer().init(index));
		index = index + 1;
	}
	println("OK");

	print("Vérification du tableau... ");
	index = 0;
	while (index < taille) {
		if (cast<Integer>(tab.get(index)).val != index) {
			println("Erreur : données du tableau incorrectes");
			0/0;
		}
		index = index + 1;
	}
	println("OK");
}
